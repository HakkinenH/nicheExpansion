model

{
  # Likelihood
  for (t in 1:N) {
    y[t] ~ dbeta(a[t], b[t])
  
    y_pred[t] ~ dbeta(a[t], b[t])

    #extract loglikelihood (needed for WAIC)
    LogLik[t] <- log(dbeta(y[t], a[t], b[t]))
  
    a[t] <- mu[t] * phi[t]
    b[t] <- (1 - mu[t]) * phi[t]

    logit(mu[t]) <- alpha[cat1[t]] + 
                    beta1[cat1[t]] * x1[t]
                    

    #I think variance varies across height and intro_lag, so allow phi to vary linearly. 
    log(phi[t]) <- phi_alpha + phi_beta * x1[t] 

    #to calculate Pearson residuals:
    varY[t] <- mu[t]*(1-mu[t])
    
    PRes[t] <- (y[t] - mu[t]) / sqrt(varY[t])
    PResNew[t] <- (y_pred[t] - mu[t]) / sqrt(varY[t])
    
    D[t] <- pow(PRes[t],2)
    DNew[t] <- pow(PResNew[t],2)

  }
  
  # Priors
  for(j in 1:N_cat1){
        alpha[j] ~ dnorm(mu_alpha, sigma_alpha^-2)
        beta1[j] ~ dnorm(mu_beta1, sigma_beta1^-2)
  }


  
  # Priors
  mu_alpha ~ dnorm(-1, 2^-2)
  mu_beta1 ~ dnorm(0, 1^-2)


  #used to be dunif but half cauchy is more conservative (tries to minimise differences)
  sigma_alpha ~ dt(0,5,1)T(0,)
  sigma_beta1 ~ dt(0,5,1)T(0,)
  
  #with variable variance, phi needs priors
  #phi ~ dunif(0, 10)
  phi_alpha ~ dunif(0, 10)
  phi_beta ~ dnorm(0, 2^-2)


  Fit <- sum(D[1:N])
  FitNew <-sum(DNew[1:N]) 

  pvalue <- mean(FitNew>Fit)

}